plugins {
  id "groovy"
}

repositories {
  mavenCentral()
}

// The the serviceportale public API is maintained by the "SchildkrÃ¶ten" Team and the most recent version listed on
// maven central: https://central.sonatype.com/artifact/de.seitenbau.serviceportal/prozess-scripting-api
String serviceportalPublicApiVersion = "1.195.8"

ext {
  groovyVersion = project.hasProperty("groovyVersion") ? project.groovyVersion : "2.5.22"
  spockVersion = groovyVersion == "2.5.22" ? "2.0-M1-groovy-2.5" : "2.4-M6-groovy-4.0"
  groovyGroup = groovyVersion.startsWith("4.") ? "org.apache.groovy" : "org.codehaus.groovy"
}

dependencies {
  [
          "${groovyGroup}:groovy-all:${groovyVersion}",
          "de.seitenbau.serviceportal:prozess-scripting-api:${serviceportalPublicApiVersion}",
          "org.activiti:activiti-engine:5.22.0",
          "org.apache.commons:commons-text:1.13.0"

  ].each { dep ->
    implementation(dep) {
      // Required to remove unneeded logging frameworks that break the build
      // See: https://stackoverflow.com/a/59667631/3298787
      exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
  }

  // testing
  implementation group: 'net.bytebuddy', name: 'byte-buddy-gradle-plugin', version: '1.10.1'
  testImplementation "org.spockframework:spock-core:${spockVersion}"

  // Allow IntelliJ to load groovy grapes. Not required for scripts in the commons repo, but
  // required for developing the scripts in the 'tools' folder.
  implementation(group: 'org.apache.ivy', name: 'ivy', version: '2.4.0')
}

sourceSets {
  main {
    groovy {
      srcDirs = ['scripts']
    }
  }

  test {
    groovy {
      if (groovyVersion.startsWith("4.")) {
        srcDirs = ['test/processEngineV2']
      } else {
        srcDirs = ['test/processEngineV1']
      }
    }
    resources {
      srcDirs = ["test"]
    }
  }
}

test {
  // Enable logging of test results
  testLogging {
    events "passed", "skipped", "failed"
  }

  // Enforce JUnit for Spock runs (see https://blog.solidsoft.pl/2020/01/02/migrating-spock-1.3-tests-to-spock-2.0/ )
  useJUnitPlatform()

  // Fail the build if no rests were run.
  afterSuite { desc, result ->
    if (!desc.parent) {
      if (result.testCount == 0) {
        throw new IllegalStateException("No tests were found. Failing the build")
      }
    }
  }
}
